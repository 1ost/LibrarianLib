buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.61"
    }
}

allprojects {
    apply plugin: 'net.minecraftforge.gradle'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'maven-publish'
    apply plugin: 'kotlin'

    ext.allmodules = rootProject.subprojects.findAll { it.path != ":runtime" }

    version = '1.0'
    group = mod_group
    archivesBaseName = mod_id + (project == rootProject ? "" : "-${project.name}")

    sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

    repositories {
        jcenter()
        maven { url = "http://maven.shadowfacts.net/" }
        maven { url = "https://jitpack.io" }
        maven { url = "https://minecraft.curseforge.com/api/maven/" }
    }

    minecraft {
        mappings channel: mc_mappings_channel, version: mc_mappings_version
    }

    configurations {
        shade
        api.extendsFrom(libApi)
        implementation.extendsFrom(libImplementation)
        testImplementation.extendsFrom(libTestImplementation)
        libRuntime.extendsFrom(libApi, libImplementation, libTestImplementation)
    }

    dependencies {
        minecraft "net.minecraftforge:forge:$mc_version-$forge_version"
        implementation "kottle:Kottle:$kottle_version"
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = "1.8"
            freeCompilerArgs += [
                "-Xjvm-default=enable",
                "-Xuse-experimental=kotlin.Experimental"
            ]
        }
    }
}

configure(allmodules) {
    def resourceRoots = project.sourceSets.main.resources.sourceDirectories
    def testResourceRoots = project.sourceSets.test.resources.sourceDirectories

    if(path != ":core") {
        task generateSkeletonModInfo << {
            def output = sourceSets.main.output.resourcesDir

            def tomlString = """\
                modLoader="kotlinfml"
                loaderVersion="[1,)"
                [[mods]]
                modId="librarianlib-$project.name"
                version="${rootProject.mod_version}"
                displayName="LibrarianLib $humanName"
                description="The LibrarianLib $humanName module"
                [[dependencies.librarianlib-$project.name]]
                    modId="librarianlib"
                    versionRange="[${rootProject.mod_version}]"
                    mandatory=true
            """.stripIndent()

            // add module dependencies, so missing modules will produce meaningful errors
            project.compileJava.taskDependencies.getDependencies().collect { it.project }.unique()
                    .each { dep ->
                        if(dep == project || dep.path == ":core")
                            return
                        tomlString += """\
                            [[dependencies.librarianlib-$project.name]]
                                modId="librarianlib-$dep.name"
                                versionRange="[${rootProject.mod_version}]"
                                mandatory=true
                        """.stripIndent()
                    }

            resourceRoots.collect { file(it.path + "/META-INF/dependencies.toml") }.findAll { it.exists() }.each {
                def dep = it.text.replaceAll("MOD_NAME", "librarianlib-$project.name")
                if(!dep.endsWith("\n"))
                    dep += "\n"
                tomlString += dep
            }

            def packMcMeta = """\
                {
                    "pack": {
                        "description": "LibrarianLib $humanName resources",
                        "pack_format": 4
                    }
                }
            """.stripIndent()

            file(output.path + '/META-INF').mkdirs()
            file(output.path + '/META-INF/mods.toml').text = tomlString
            file(output.path + '/pack.mcmeta').text = packMcMeta
        }

        processResources.finalizedBy(generateSkeletonModInfo)
    }

    task generateTestModInfo << {
        def output = sourceSets.test.output.resourcesDir

        def tomlString = """\
            modLoader="kotlinfml"
            loaderVersion="[1,)"
            [[mods]]
            modId="librarianlib-$project.name-test"
            version="${rootProject.mod_version}"
            displayName="LibrarianLib $humanName Test"
            description="The LibrarianLib $humanName test mod"
            [[dependencies.librarianlib-$project.name-test]]
                modId="librarianlib"
                versionRange="[${rootProject.mod_version}]"
                mandatory=true
        """.stripIndent()


        // add module dependencies, so missing modules will produce meaningful errors
        project.compileTestJava.taskDependencies.getDependencies().collect { it.project }.unique()
                .each { dep ->
                    if(dep.path == ":core")
                        return
                    tomlString += """\
                            [[dependencies.librarianlib-$project.name-test]]
                                modId="librarianlib-$dep.name"
                                versionRange="[${rootProject.mod_version}]"
                                mandatory=true
                        """.stripIndent()
                }

        testResourceRoots.collect { file(it.path + "/META-INF/dependencies.toml") }.findAll { it.exists() }.each {
            def dep = it.text.replaceAll("MOD_NAME", "librarianlib-$project.name-test")
            if(!dep.endsWith("\n"))
                dep += "\n"
            tomlString += dep
        }

        def packMcMeta = """\
            {
                "pack": {
                    "description": "LibrarianLib $humanName test resources",
                    "pack_format": 4
                }
            }
        """.stripIndent()

        file(output.path + '/META-INF').mkdirs()
        file(output.path + '/META-INF/mods.toml').text = tomlString
        file(output.path + '/pack.mcmeta').text = packMcMeta
    }

    processTestResources.finalizedBy(generateTestModInfo)
}

ext.productionModules = allmodules.findAll { it.findProperty("developmentOnly") != "true" }

configurations {
    implementation.extendsFrom libModule
}

dependencies {
    productionModules.each {
        implementation(it)
        shade(project(path: it.path, configuration: "shade"))
    }
}

productionModules.each {
    classes.dependsOn(it.classes)
    processResources.dependsOn(it.processResources)
    jar.dependsOn(it.jar)
}

minecraft {
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.

    // accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
}

//processResources {
//    productionModules.each { mod ->
//        if(mod.properties.get("developmentOnly") == "true") return
//
//        from(mod.sourceSets.main.output.resourcesDir) {
//            if(mod.path == ":core") {
//                filesMatching('META-INF/modules/index.txt') {
//                    filter { line ->
//                        if (project(":$line").properties.get("developmentOnly") == "true")
//                            return null
//                        return line
//                    }
//                }
//            } else {
//                exclude 'META-INF/coremods.json'
//                exclude 'META-INF/mods.toml'
//                exclude 'META-INF/dependencies.toml'
//            }
//        }
//    }
//}


task mergeClasses(type: Copy) {
    productionModules.each { mod ->
        from(mod.sourceSets.main.output.classesDirs) {
            exclude '**/LibModuleSkeleton.class'
        }
    }
    into(file("$buildDir/merged/classes"))
}

task mergeResources(type: Copy) {
    ext.outputDir = file("$buildDir/merged/resources")
    productionModules.each { mod ->
        from(mod.sourceSets.main.output.resourcesDir) {
            if (mod.path == ":core") {
                filesMatching('META-INF/modules/index.txt') {
                    filter { line ->
                        if (project(":$line").findProperty("developmentOnly") == "true")
                            return null
                        return line
                    }
                }
            } else {
                exclude 'META-INF/coremods.json'
                exclude 'META-INF/mods.toml'
                exclude 'META-INF/dependencies.toml'
            }
        }
    }
    into(outputDir)

    doLast { // do final post-processing to merge the mods
        ext.outputDir = file("$buildDir/merged/resources")

        def resources = files([])
        productionModules.each { resources.from(it.sourceSets.main.resources.sourceDirectories) }

        file("$outputDir/META-INF").mkdirs()

        // merge coremods
        def coremodsJson = file("$outputDir/META-INF/coremods.json")
        def allCoremods = resources.collect { file(it.path + "/META-INF/coremods.json") }.findAll { it.exists() }

        coremodsJson.text =
                "{\n" + allCoremods.collect {
                    it.text
                            .replaceAll("[{}]", "") // replace enclosing brackets with blank lines
                            .replaceAll("\\s+\$", "") // remove trailing whitespace
                            .split("\n").findAll { !it.matches("^\\s*\$") } // remove blank lines
                            .join("\n") // re-join lines
                }.join(",\n") + "\n}"

        // add additional external dependencies to main mods.toml
        def additionalDependencies = resources
                .collect { file(it.path + "/META-INF/dependencies.toml") }.findAll { it.exists() }
                .collect {
                    it.text
                            .replaceAll("MOD_NAME", "librarianlib")
                            .replaceAll("\\s+\$", "")
                }.join("\n")
        def modsToml = file("$outputDir/META-INF/mods.toml")
        modsToml.text = modsToml.text + additionalDependencies
    }
}

jar {
    configurations.shade.each { dep ->
        from(zipTree(dep)) {
            exclude 'META-INF', 'META-INF/**'
        }
    }
    from(mergeClasses)
    from(mergeResources)
}

// Example for how to get properties into the manifest for reading by the runtime..
//jar {
//    manifest {
//        attributes([
//                "Specification-Title": "examplemod",
//                "Specification-Vendor": "examplemodsareus",
//                "Specification-Version": "1", // We are version 1 of ourselves
//                "Implementation-Title": project.name,
//                "Implementation-Version": "${version}",
//                "Implementation-Vendor" :"examplemodsareus",
//                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
//        ])
//    }
//}

// Example configuration to allow publishing using the maven-publish task
// we define a custom artifact that is sourced from the reobfJar output task
// and then declare that to be published
// Note you'll need to add a repository here
def reobfFile = file("$buildDir/reobfJar/output.jar")
def reobfArtifact = artifacts.add('default', reobfFile) {
    type 'jar'
    builtBy 'reobfJar'
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact reobfArtifact
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}
