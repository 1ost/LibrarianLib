import net.minecraftforge.gradle.common.util.RunConfig

buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.61"
    }
}

allprojects {
    apply plugin: 'net.minecraftforge.gradle'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'maven-publish'
    apply plugin: 'kotlin'

    version = '1.0'
    group = mod_group
    archivesBaseName = mod_id

    sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

    repositories {
        maven { url = "http://maven.shadowfacts.net/" }
        maven { url = "https://jitpack.io" }
    }

    minecraft {
        mappings channel: mc_mappings_channel, version: mc_mappings_version
    }

    dependencies {
        minecraft "net.minecraftforge:forge:$mc_version-$forge_version"
        implementation "org.jetbrains.kotlin:kotlin-stdlib"
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = "1.8"
            freeCompilerArgs += [
                "-Xjvm-default=enable",
                "-Xuse-experimental=kotlin.Experimental"
            ]
        }
    }
}

minecraft {
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.

    // accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
    Closure runDefaults = {
        workingDirectory project.file('run')

        property 'forge.logging.markers', 'REGISTRIES'
        property 'forge.logging.console.level', 'debug'
        ideaModule 'librarianlib.main' // ForgeGradle generates `librarianlib_main` by default, which doesn't work.

        mods { // NamedDomainObjectContainer<ModConfig>
            create(name) {
                source sourceSets.main
            }
            subprojects.each { mod ->
                create("librarianlib-${mod.name}") {
                    source mod.sourceSets.main
                }
                create("librarianlib-${mod.name}-test") {
                    source mod.sourceSets.test
                }
            }
        }
    }

    runs { // NamedDomainObjectContainer<RunConfig>
        client { // RunConfig
            configure(delegate, runDefaults)
        }

        server {
            configure(delegate, runDefaults)
        }

        data {
            configure(delegate, runDefaults)

            args '--mod', 'librarianlib', '--all', '--output', file('src/generated/resources/')
        }
    }
}

dependencies {
    subprojects.each {
        compileOnly(it)
        compileOnly(it.sourceSets.test.output)
    }
}

// Example for how to get properties into the manifest for reading by the runtime..
//jar {
//    manifest {
//        attributes([
//                "Specification-Title": "examplemod",
//                "Specification-Vendor": "examplemodsareus",
//                "Specification-Version": "1", // We are version 1 of ourselves
//                "Implementation-Title": project.name,
//                "Implementation-Version": "${version}",
//                "Implementation-Vendor" :"examplemodsareus",
//                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
//        ])
//    }
//}

// Example configuration to allow publishing using the maven-publish task
// we define a custom artifact that is sourced from the reobfJar output task
// and then declare that to be published
// Note you'll need to add a repository here
def reobfFile = file("$buildDir/reobfJar/output.jar")
def reobfArtifact = artifacts.add('default', reobfFile) {
    type 'jar'
    builtBy 'reobfJar'
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact reobfArtifact
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}
