import org.gradle.internal.os.OperatingSystem
switch (OperatingSystem.current()) {
    case OperatingSystem.LINUX:
        ext.lwjglNatives = "natives-linux"
        break
    case OperatingSystem.MAC_OS:
        ext.lwjglNatives = "natives-macos"
        break
    case OperatingSystem.WINDOWS:
        ext.lwjglNatives = "natives-windows"
        break
}

dependencies {
    api project(":core")
    api project(":mosaic")
    api project(":albedo")
    api project(":etcetera")
    api project(":prism")
    api project(":courier")
    testApi project(":testbase")
    shade("dev.thecodewarrior:bitfont:$bitfont_version") {
        exclude group: 'org.jetbrains.kotlin'
        exclude group: 'it.unimi.dsi' // fastutil is already included in MC
    }
    shade 'com.ibm.icu:icu4j:63.1'
    shade 'org.msgpack:msgpack-core:0.8.16'

    // LibLib won't work in production until https://github.com/cpw/modlauncher/issues/27 is resolved, but by
    // including yoga as a normal maven dependency at least it'll work in dev until that issue is fixed
    api("org.lwjgl:lwjgl-yoga:$yoga_version:natives-linux") {
        exclude group: 'org.lwjgl', module: 'lwjgl'
    }
    api("org.lwjgl:lwjgl-yoga:$yoga_version:natives-macos") {
        exclude group: 'org.lwjgl', module: 'lwjgl'
    }
    api("org.lwjgl:lwjgl-yoga:$yoga_version:natives-windows") {
        exclude group: 'org.lwjgl', module: 'lwjgl'
    }
    api("org.lwjgl:lwjgl-yoga:$yoga_version") {
        exclude group: 'org.lwjgl', module: 'lwjgl'
    }

    // we have to include ICU in the classpath so it overrides Mojang's stripped down copy
    devClasspath 'com.ibm.icu:icu4j:63.1'
    // we have to include yoga in the classpath because forge bypasses mod classloaders when getting anything under the
    // `org.lwjgl` package (https://github.com/cpw/modlauncher/issues/27)
    devClasspath "org.lwjgl:lwjgl-yoga:$yoga_version:$lwjglNatives"
    devClasspath "org.lwjgl:lwjgl-yoga:$yoga_version"

    // Compatibility:

    compileOnly fg.deobf("mezz.jei:jei-${mc_version}:${jei_version}:api")
    mod fg.deobf("mezz.jei:jei-${mc_version}:${jei_version}")
    compileOnly "curse.maven:mouse-tweaks-60089:${mousetweaks_api_id}"
    mod fg.deobf("curse.maven:mouse-tweaks-60089:${mousetweaks_jar_id}")
    mod fg.deobf("curse.maven:inventory-sorter-240633:${inventorysorter_jar_id}")
}
