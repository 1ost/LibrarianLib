import org.gradle.internal.os.OperatingSystem
switch (OperatingSystem.current()) {
    case OperatingSystem.LINUX:
        ext.lwjglNatives = "natives-linux"
        break
    case OperatingSystem.MAC_OS:
        ext.lwjglNatives = "natives-macos"
        break
    case OperatingSystem.WINDOWS:
        ext.lwjglNatives = "natives-windows"
        break
}

dependencies {
    api project(":core")
    api project(":mosaic")
    api project(":albedo")
    api project(":etcetera")
    api project(":prism")
    api project(":courier")
    testApi project(":testbase")
    shade("com.github.thecodewarrior:bitfont:proper-typesetter-SNAPSHOT") {
        exclude group: 'org.jetbrains.kotlin'
        exclude group: 'it.unimi.dsi' // already included in MC
    }
    shade 'com.ibm.icu:icu4j:63.1'
    shade 'org.msgpack:msgpack-core:0.8.16'

    // LibLib won't work in production until https://github.com/cpw/modlauncher/issues/27 is resolved, but by
    // including yoga as a normal maven dependency at least it'll work in dev until that issue is fixed
    api('org.lwjgl:lwjgl-yoga:3.2.1:natives-linux') {
        exclude group: 'org.lwjgl', module: 'lwjgl'
    }
    api('org.lwjgl:lwjgl-yoga:3.2.1:natives-macos') {
        exclude group: 'org.lwjgl', module: 'lwjgl'
    }
    api('org.lwjgl:lwjgl-yoga:3.2.1:natives-windows') {
        exclude group: 'org.lwjgl', module: 'lwjgl'
    }
    api('org.lwjgl:lwjgl-yoga:3.2.1') {
        exclude group: 'org.lwjgl', module: 'lwjgl'
    }

    // we have to include ICU in the classpath so it overrides Mojang's stripped down copy
    devClasspath 'com.ibm.icu:icu4j:63.1'
    // we have to include yoga in the classpath because forge bypasses mod classloaders when getting anything under the
    // `org.lwjgl` package (https://github.com/cpw/modlauncher/issues/27)
    devClasspath "org.lwjgl:lwjgl-yoga:3.2.1:$lwjglNatives"
    devClasspath 'org.lwjgl:lwjgl-yoga:3.2.1'
}

shadePackages(
        'com.ibm.icu',
        'dev.thecodewarrior.bitfont',
        'org.msgpack',
)
